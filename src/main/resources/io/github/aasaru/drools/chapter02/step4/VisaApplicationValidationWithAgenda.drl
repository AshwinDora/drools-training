package io.github.aasaru.drools.chapter03

import io.github.aasaru.drools.domain.Passport;
import io.github.aasaru.drools.domain.VisaApplication;

import java.time.LocalDate;

rule "Invalidate visa application where passport expires earlier than 6 months after visit end date"
  dialect "mvel"
  agenda-group "invalid-application"
  when
    $passport: Passport()
    $visaApplication: VisaApplication( passportNumber == $passport.passportNumber,
                                       $passport.expiresOn.isBefore(visitEndDate.plusMonths(6)) )
  then
    System.out.println( "Set " + $visaApplication + " invalid as " + $passport + " is not valid 6 months after planned visit end date");
    modify($visaApplication) {
      setValidation(false)
    }
end

rule "Invalidate visa application with invalid passport"
  dialect "mvel"
  agenda-group "invalid-application"
  when
    $passport: Passport( validationResult == false )
    $visaApplication: VisaApplication( passportNumber == $passport.passportNumber )
  then
    System.out.println( "Set " + $visaApplication + " invalid as " + $passport + " hasn't passed validation");
    modify($visaApplication) {
      setValidation(false)
    }
end

rule "Set application without validationPassed info as valid"
  dialect "mvel"
  agenda-group "valid-application"
  when
    $visaApplication: VisaApplication( validationPassed == null )
  then
    System.out.println( $visaApplication + " is without validation info, consider OK" );
    modify($visaApplication) {
      setValidation(true)
    }
end





// we copied over one more drl file from previous chapter
// from org.github.aasaru.drools.chapter03.step
//
// when we run this, we get what we want as a result but there are a lot of log messages