package io.github.aasaru.drools.chapter06

import io.github.aasaru.drools.domain.InvalidPassport;
import io.github.aasaru.drools.domain.Passport;
import io.github.aasaru.drools.domain.InvalidFamilyVisaApplication;
import io.github.aasaru.drools.domain.FamilyVisaApplication;
import io.github.aasaru.drools.domain.Visa;

import java.time.LocalDate;

rule "Invalidate visa applications with invalid passport"
    dialect "mvel"
    when
        $familyVisaApplication: FamilyVisaApplication( )
        exists InvalidPassport( $familyVisaApplication.passportNumbers.contains(this.passport.passportNumber) )
    then
        System.out.println( "Set " + $familyVisaApplication + " invalid as it contains invalid passport");
        insert( new InvalidFamilyVisaApplication($familyVisaApplication) )
end

rule "Invalidate visa application where everyone is underage"
    dialect "mvel"
    when
        $familyVisaApplication: FamilyVisaApplication( )
        forall (
          $passport : Passport( $familyVisaApplication.passportNumbers.contains(this.passportNumber) ),
                      Passport( this == $passport, underage == true)
        )
    then
        System.out.println( "Set " + $familyVisaApplication + " invalid as everyone is underage in it.");
        insert( new InvalidFamilyVisaApplication($familyVisaApplication) )
end
