package io.github.aasaru.drools.chapter05

import io.github.aasaru.drools.domain.InvalidPassport;
import io.github.aasaru.drools.domain.InvalidVisaApplication;
import io.github.aasaru.drools.domain.Passport;
import io.github.aasaru.drools.domain.Validation;
import io.github.aasaru.drools.domain.ValidVisaApplication;
import io.github.aasaru.drools.domain.VisaApplication;
import java.time.LocalDate
import io.github.aasaru.drools.domain.Visa;

rule "Invalidate visa application where passport expires earlier than 6 months after visit end date"
  dialect "mvel"
  salience -50
  when
    $passport: Passport()
    $visaApplication: VisaApplication( this.passportNumber == $passport.passportNumber,
                                       $passport.expiresOn.isBefore(this.visitEndDate.plusMonths(6)) )
  then
    System.out.println( "Set " + $visaApplication + " invalid as " + $passport + " is not valid 6 months after planned visit end date");
    insert( new InvalidVisaApplication($visaApplication) )
end

rule "Issue visa for valid applications"
    dialect "mvel"
    when
      $visaApplication: VisaApplication( )
      not ( InvalidPassport( this.passport.passportNumber == $visaApplication.passportNumber ) )
      not ( InvalidVisaApplication( this.visaApplication == $visaApplication ) )
    then
      Visa visa = new Visa( $visaApplication.getPassportNumber() );
      System.out.println("Issue " + visa);
      insertLogical( visa )
end